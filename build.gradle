plugins {
    id 'application'
    id 'org.beryx.jlink' version '3.0.1'
    id 'io.github.jwharm.flatpak-gradle-generator' version '1.2.0'
}

group = 'com.toxicstoxm.LEDSuite'
version = '0.1.4'

repositories {
    maven { url './offline-repository/' }
    mavenCentral()
}

dependencies {
    implementation 'io.github.jwharm.javagi:adw:0.10.2'

    implementation 'org.yaml:snakeyaml:2.2'

    implementation 'org.jetbrains:annotations:24.1.0'
    annotationProcessor 'org.jetbrains:annotations:24.1.0'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

tasks.register('glibCompileResources') {
    doLast {
        exec {
            workingDir 'src/main/resources'
            commandLine 'glib-compile-resources', 'LEDSuite.gresource.xml'
        }
    }
}

tasks.named('classes') {
    dependsOn glibCompileResources
}

application {
    mainClass = 'com.toxicstoxm.LEDSuite.Main'
    mainModule = 'com.toxicstoxm.LEDSuite'
    applicationDefaultJvmArgs = [
            '--enable-native-access=org.gnome.glib',
            '--enable-native-access=org.gnome.gobject',
            '--enable-native-access=org.gnome.gio',
            '--enable-native-access=org.gnome.gdk',
            '--enable-native-access=org.gnome.gtk',
            '--enable-native-access=org.gnome.adw',
            '--enable-native-access=org.freedesktop.cairo',
            '--enable-native-access=com.toxicstoxm.LEDSuite'
    ]
}

jlink {
    options = [
            '--strip-debug',
            '--no-header-files',
            '--no-man-pages',
    ]
    launcher {
        name = 'LEDSuite'
    }
}

tasks.flatpakGradleGenerator {
    outputFile = file('flatpak-sources.json')
    downloadDirectory = './offline-repository'
}


tasks.jar {
    dependsOn('fatJar')
    manifest {
        attributes['Main-Class'] = 'com.toxicstoxm.LEDSuite.Main'
    }
}

tasks.register('fatJar', Jar) {
    manifest {
        attributes['Main-Class'] = 'com.toxicstoxm.LEDSuite.Main'
    }

    archiveBaseName = "${rootProject.name}-fat"
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}